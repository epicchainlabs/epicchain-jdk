plugins {
    id "de.undercouch.download" version "4.1.2"
}

description 'NeoFS Java API'

repositories {
    mavenCentral()
    // Remove this once neofs-api-java is officially released
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
}

dependencies {
    compile project(":core")

    compile "io.neow3j:neofs-api-java:$neofsApiJavaVersion",
            "com.google.protobuf:protobuf-java-util:$protobufJavaUtilVersion"

    implementation "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion",
            "net.java.dev.jna:jna-platform:$jnaVersion",
            "org.awaitility:awaitility:$awaitility"
}

configurations {
    testArtifacts.extendsFrom testRuntime
}

artifacts {
    testArtifacts testJar
}

// Todo: build task of this module should depend on the download tasks below
// Todo: Include all the .so files inside the jar (might be automatically done, but need to check)

task downloadLibZip(type: Download) {
    src "https://github.com/neow3j/neofs-api-shared-lib/releases/download/$neofsSharedLibVersion/libneofs-all.zip"
    dest new File(temporaryDir, 'libneofs-all.zip')
    onlyIfModified true
}

task downloadAndUnzipLib(dependsOn: downloadLibZip, type: Copy) {
    from zipTree(downloadLibZip.dest)
    into sourceSets.main.output.resourcesDir
}

jar {
    dependsOn downloadAndUnzipLib
    from sourceSets.main.output.resourcesDir
}

build {
    dependsOn downloadAndUnzipLib
}
